buildscript {
    repositories {
        jcenter()
        maven { url "http://dl.bintray.com/jfrog/jfrog-jars" }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:2.2.3"
    }
}

apply plugin: "artifactory"
apply from: "gradle/idea/idea.gradle"

artifactory {
    contextUrl = "http://oss.jfrog.org/"
}

allprojects {
    version = "2.0.0"

    plugins.withType(JavaPlugin) {
        sourceCompatibility = "1.6"
        targetCompatibility = "1.6"
    }

    ext {
        isSnapshot = version.endsWith("-SNAPSHOT")

        commonDependencies = [
            cglib: dependencies.create("cglib:cglib-nodep:2.2.2"),
            groovy: dependencies.create("org.codehaus.groovy:groovy-all:2.3.3:indy"),
            guava: dependencies.create("com.google.guava:guava:15.0"),
            httpBuilder: dependencies.create("org.codehaus.groovy.modules.http-builder:http-builder:0.6", {
                exclude module: "groovy"
                exclude module: "httpclient"
            }),
            httpClient: dependencies.create("org.apache.httpcomponents:httpclient:4.3.1"),
            junit: dependencies.create("junit:junit:4.10"),
            littleProxy: dependencies.create("org.littleshoot:littleproxy:1.0.0-beta4"),
            netty: dependencies.create("io.netty:netty-all:4.0.11.Final"),
            snakeYaml: dependencies.create("org.yaml:snakeyaml:1.16"),
            spock: dependencies.create("org.spockframework:spock-core:0.7-groovy-2.0", {
                exclude module: "groovy-all"
                exclude module: "junit-dep"
            }),
            tika: dependencies.create("org.apache.tika:tika-core:1.11")
        ]
    }
}

