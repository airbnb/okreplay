apply plugin: 'groovy'
apply plugin: 'codenarc'
apply plugin: 'nexus'
apply plugin: 'github-pages'

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
		classpath 'org.ajoberstar:gradle-git:0.2.0'
	}
}

version = '1.2-SNAPSHOT'
group = 'co.freeside'
archivesBaseName = 'betamax'

repositories {
	mavenCentral()
	mavenRepo url: 'http://repository.codehaus.org'
}

dependencies {
	groovy 'org.codehaus.groovy:groovy-all:1.8.8'

	compile 'junit:junit:4.8.2',
			'org.yaml:snakeyaml:1.10',
			'org.apache.httpcomponents:httpclient:4.2.2',
			'commons-lang:commons-lang:2.4',
			'org.eclipse.jetty:jetty-server:7.3.1.v20110307'

	testCompile('org.codehaus.groovy.modules.http-builder:http-builder:0.6') {
		exclude module: 'groovy'
		exclude module: 'httpclient'
	}
	testCompile('org.spockframework:spock-core:0.7-groovy-1.8') {
		exclude module: 'groovy-all'
		exclude module: 'junit-dep'
	}
	testCompile 'commons-httpclient:commons-httpclient:3.1',
				'com.github.groovy-wslite:groovy-wslite:0.7.1'

	testRuntime 'cglib:cglib-nodep:2.2.2'
}

modifyPom {
	dependencies.removeAll(dependencies.findAll { it.scope == 'test' })

	project {
		name 'Betamax - An HTTP stubbing proxy for testing JVM applications.'
		packaging 'jar'
		description 'Betamax is a Groovy record/playback proxy for stubbing access to external HTTP resources when testing. Inspired by Ruby\'s VCR.'
		url 'http://freeside.co/betamax'
		inceptionYear '2011'

		scm {
			url 'scm:git@github.com:robfletcher/betamax.git'
			connection 'scm:git@github.com:robfletcher/betamax.git'
			developerConnection 'scm:git@github.com:robfletcher/betamax.git'
		}

		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}

		developers {
			developer {
				id 'robfletcher'
				name 'Rob Fletcher'
				url 'http://blog.freeside.co/'
				roles {
					role 'Lead'
					role 'Founder'
				}
			}
		}
	}
}

nexus {
	sign = true
}

codenarc {
	toolVersion = '0.17'
	ignoreFailures = true
}

groovydoc {
	docTitle = "Betamax $version"
	windowTitle = "Betamax $version"
	footer = """<p style="text-align: center">API Documentation for Betamax $version - Licensed under the Apache License, Version 2.0 - <a href="http://freeside.co/betamax">http://freeside.co/betamax</a></p>"""

	def apiUrls = [
			'http://download.oracle.com/javase/7/docs/api',
			'http://groovy.codehaus.org/api',
			'http://junit.sourceforge.net/javadoc',
			'http://snakeyamlrepo.appspot.com/releases/1.10/site/apidocs',
			'http://hc.apache.org/httpcomponents-client-ga/httpclient/apidocs'
	]
	apiUrls.each {
		def packages = new URL("$it/package-list").text.tokenize('\n')
		link(it, *packages)
	}
}

githubPages {
	repoUri = 'git@github.com:robfletcher/betamax.git'
	workingPath = "$buildDir/public"
	pages {
		exclude '**/_site/**'
		from('src/docs') {
			exclude '**/*.less'
		}
		from(groovydoc.outputs.files) {
			into 'api'
		}
	}
}

test {
	testLogging {
		quiet {
			events 'failed'
			exceptionFormat 'short'
		}
	}
}
